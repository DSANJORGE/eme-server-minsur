import groovy.json.JsonOutput
import org.entermediadb.asset.MediaArchive
import org.openedit.Data

void audit(MediaArchive archive, String action, String targettype, String targetid, Object before, Object after) {
  def s = archive.getSearcher("auditevent")
  Data e = s.createNewData() // ponytail: no setId - BaseElasticSearcher.nextId() always throws ("Should not call next ID"); saveData below auto-assigns an ES id when none is set
  e.setValue("datecreated", new Date())
  e.setValue("actor", context.getUser()?.getId())
  e.setValue("action", action)
  e.setValue("targettype", targettype)
  e.setValue("targetid", targetid)
  e.setValue("before", before == null ? "" : JsonOutput.toJson(before))
  e.setValue("after", after == null ? "" : JsonOutput.toJson(after))
  s.saveData(e, context.getUser())
}
void reply(Map m) { context.putPageValue("json", JsonOutput.toJson(m)) }
void fail(int code, String msg) { context.getResponse().setStatus(code); reply([ok: false, error: msg]); context.setCancelActions(true) }
Map snapshot(Data d, List fields) { d == null ? null : fields.collectEntries { [(it): d.get(it)] } }

MediaArchive archive = context.getPageValue("mediaarchive")
String userid = (context.getRequestParameter("userid") ?: "").trim().toLowerCase(); String role = context.getRequestParameter("role")
if (!(role in ["users", "manager", "training", "orgadmin"])) { fail(400, "invalid role"); return }
if (archive.getSearcher("user").searchById(userid) == null) { fail(404, "no user"); return }
def profiles = archive.getSearcher("userprofile")
Data p = profiles.searchById(userid) ?: profiles.createNewData()
String before = p.get("settingsgroup")
p.setId(userid); p.setValue("userid", userid); p.setValue("settingsgroup", role)
profiles.saveData(p, context.getUser())
// eMe keeps loaded profiles in CacheManager("userprofile"); without this the old role
// survives until Tomcat restarts (UserProfileManager.setRoleOnUser does the same).
archive.getUserProfileManager().clearProfile(archive.getCatalogId(), userid)
audit(archive, "user.role", "user", userid, [role: before], [role: role])
reply([ok: true])
